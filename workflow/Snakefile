# Snakefile
from snakemake.io import glob_wildcards

configfile: "config/config_STS_Q.yml"
#configfile: "scripts/testing/config_test_BS.yaml"
#configfile: "scripts/testing/regression_testing/config_BS_reg_test.yml"


ROOT = config['dataset']["root_dir"]
DERIV = config['dataset']["derivatives_subfolder"]

def get_imagerecon_output(): # create file name for image recon output
    return (
        f"{ROOT}/derivatives/{DERIV}/image_results/sub-{{subject}}/"
        f"Xs_sub-{{subject}}_{{task}}"
        f"_cov_alpha_spatial_{config['image_recon']['alpha_spatial']}"
        f"_alpha_meas_{config['image_recon']['alpha_meas']}"
        + ("_direct" if config["image_recon"]["DIRECT"]["enable"] else "_indirect")
        + ("_Cmeas" if config["image_recon"]["Cmeas"]["enable"] else "_noCmeas")
        + ("_SB" if config["image_recon"]["spatial_basis"]["enable"] else "_noSB")
        + ("_mag" if config["image_recon"]["mag"]["enable"] else "_ts")
        + (f"_{config['image_recon']['mag']['t_win'][0]}_{config['image_recon']['mag']['t_win'][1]}" if config["image_recon"]["mag"]["enable"] else "")
        + ".pkl.gz"
    )

def get_groupavg_imagerecon_output():
    return (
        f"{ROOT}/derivatives/{DERIV}/image_results/"
        f"Xs_groupavg_{{task}}"
        f"_cov_alpha_spatial_{config['image_recon']['alpha_spatial']}"
        f"_alpha_meas_{config['image_recon']['alpha_meas']}"
        + ("_direct" if config["image_recon"]["DIRECT"]["enable"] else "_indirect")
        + ("_Cmeas" if config["image_recon"]["Cmeas"]["enable"] else "_noCmeas")
        + ("_SB" if config["image_recon"]["spatial_basis"]["enable"] else "_noSB")
        + ("_mag" if config["image_recon"]["mag"]["enable"] else "_ts")
        + (f"_{config['image_recon']['mag']['t_win'][0]}_{config['image_recon']['mag']['t_win'][1]}" if config["image_recon"]["mag"]["enable"] else "")
        + ".pkl"
    )


def get_hrf_output():
    return (
        f"{ROOT}/derivatives/{DERIV}/hrf_estimate/sub-{{subject}}/"
        f"sub-{{subject}}_task-{{task}}_nirs_hrf_estimate_{config['hrf_estimation']['rec_str']}.pkl.gz"
    )


rule all_default:
    input:
        # Groupaverage channel space 
        [expand(
            f"{ROOT}/derivatives/{DERIV}/groupaverage/"
            f"task-{{task}}_nirs_groupaverage_chanspace_{config['hrf_estimation']['rec_str']}.pkl",
            task=config["dataset"]["task"],
        ),

        # Groupaverage image space
        expand(
            get_groupavg_imagerecon_output(),
            task=config["dataset"]["task"],
        )]


rule all_groupavg_imagerecon:
    input:
        expand(get_groupavg_imagerecon_output(), task=config["dataset"]["task"])


rule all_preprocess:
    input:
        expand([
            (f"{ROOT}/derivatives/{DERIV}/preprocessed_data/sub-{{subject}}/sub-{{subject}}_task-{{task}}_run-{{run}}_nirs_preprocessed.pkl"),  #  preprocessed_dataessed snirf
            (f"{ROOT}/derivatives/{DERIV}/preprocessed_data/sub-{{subject}}/sub-{{subject}}_task-{{task}}_run-{{run}}_nirs_dataquality_geo.sidecar"), # data qual sidecar 
            ],
            subject = config["dataset"]["subject"],
            task = config["dataset"]["task"],
            run = config["dataset"]["run"]
            )
        
rule all_hrf_estimation:
    input:
        expand([
        get_hrf_output(),
            (f"{ROOT}/derivatives/{DERIV}/hrf_estimate/sub-{{subject}}/sub-{{subject}}_task-{{task}}_nirs_dataquality.json"),  # blockaverage dataqual
            (f"{ROOT}/derivatives/{DERIV}/hrf_estimate/sub-{{subject}}/sub-{{subject}}_task-{{task}}_nirs_geo.sidecar")
            ],
            subject = config["dataset"]["subject"],
            task = config["dataset"]["task"],
            )      
                      
rule all_groupaverage:
    input:
        expand([  
            (f"{ROOT}/derivatives/{DERIV}/groupaverage/task-{{task}}_nirs_groupaverage_chanspace_{config['hrf_estimation']['rec_str']}.pkl") # groupaverage
            ],
            task = config["dataset"]["task"],
            )      
            
rule all_imagerecon:
    input:
        expand(get_imagerecon_output(), subject = config["dataset"]["subject"], task=config["dataset"]["task"])

    
rule preprocess:
    input:
        # raw .snirf and events.tsv
        snirf = f"{ROOT}/sub-{{subject}}/nirs/sub-{{subject}}_task-{{task}}_run-{{run}}_nirs.snirf",
        events = f"{ROOT}/sub-{{subject}}/nirs/sub-{{subject}}_task-{{task}}_run-{{run}}_events.tsv",
        module1 = "scripts/modules/module_preprocess.py",
        module2 = "scripts/modules/module_plot_DQR.py"
    output:
        snirf = (f"{ROOT}/derivatives/{DERIV}/preprocessed_data/sub-{{subject}}/sub-{{subject}}_task-{{task}}_run-{{run}}_nirs_preprocessed.pkl"), # preprocessed snirf
        sidecar = (f"{ROOT}/derivatives/{DERIV}/preprocessed_data/sub-{{subject}}/sub-{{subject}}_task-{{task}}_run-{{run}}_nirs_dataquality_geo.sidecar"), # data qual sidecar
    params:
        cfg_preprocess = config['preprocess'],
        cfg_dataset = config['dataset'],
        stim_lst = config['hrf_estimation']['stim_lst'],
        mse_amp_thresh = config['groupaverage']['mse']['mse_amp_thresh']
    script:
        "scripts/preprocess.py"



def all_preprocessed_runs(wc):
    ''' Return a list of all preprocessed .snirf files for this subject and task (i.e. all runs)'''  
    
    run_paths =  [
        f"{ROOT}/derivatives/{DERIV}/preprocessed_data/sub-{wc.subject}/"
        f"sub-{wc.subject}_task-{wc.task}_run-{run}_nirs_preprocessed.pkl"
        for run in config["dataset"]["run"]
    ]
    data_quality_paths = [
        f"{ROOT}/derivatives/{DERIV}/preprocessed_data/sub-{wc.subject}/"
        f"sub-{wc.subject}_task-{wc.task}_run-{run}_nirs_dataquality_geo.sidecar"
        for run in config["dataset"]["run"]
    ]
    return {
        "preproc":  run_paths,
        "quality":  data_quality_paths
    }
    

rule hrf_estimation:
    input:
        preproc  = lambda wc: all_preprocessed_runs(wc)["preproc"],
        quality  = lambda wc: all_preprocessed_runs(wc)["quality"],
        module3 = "scripts/modules/module_hrf_est.py",
    output:
        # HRF for each task
        pickle = get_hrf_output(),
        json = (f"{ROOT}/derivatives/{DERIV}/hrf_estimate/sub-{{subject}}/sub-{{subject}}_task-{{task}}_nirs_dataquality.json"),
        geo = (f"{ROOT}/derivatives/{DERIV}/hrf_estimate/sub-{{subject}}/sub-{{subject}}_task-{{task}}_nirs_geo.sidecar")
    params:
        cfg_hrf = config['hrf_estimation'],
        cfg_dataset = config['dataset']
    script:
        "scripts/hrf_estimation.py"
    
    
def all_hrf_files(wc):
    ''' Return a list of all hrf .pkl files for this subject+task (i.e. all subs)'''  
    
    hrf_paths =  [
        f"{ROOT}/derivatives/{DERIV}/hrf_estimate/sub-{subject}/sub-{subject}_task-{wc.task}_nirs_hrf_estimate_{config['hrf_estimation']['rec_str']}.pkl.gz" 
        for subject in config["dataset"]["subject"]
    ]
    data_quality_paths = [
        f"{ROOT}/derivatives/{DERIV}/hrf_estimate/sub-{subject}/sub-{subject}_task-{wc.task}_nirs_dataquality.json"
        for subject in config["dataset"]["subject"]
    ]
    geo_paths = [
        f"{ROOT}/derivatives/{DERIV}/hrf_estimate/sub-{subject}/sub-{subject}_task-{wc.task}_nirs_geo.sidecar"
        for subject in config["dataset"]["subject"]
    ]
    #blockavg_nc_paths = [
     #   f"{ROOT}/derivatives/{DERIV}/hrf_estimate/sub-{subject}/sub-{subject}_task-{wc.task}_nirs_hrf_estimate.nc"
      #  for subject in config["dataset"]["subject"]
    #]
    #epoch_nc_paths = [
     #   f"{ROOT}/derivatives/{DERIV}/hrf_estimate/sub-{subject}/sub-{subject}_task-{wc.task}_nirs_epochs.nc"
     #   for subject in config["dataset"]["subject"]
    #]
    return {
        "hrf":  hrf_paths,
        "quality":  data_quality_paths, 
        "geo": geo_paths
        #"blockavg_nc": blockavg_nc_paths, 
        #"epochs_nc": epoch_nc_paths
    }
    


rule groupaverage:
    input: 
        hrf_subs  = lambda wc: all_hrf_files(wc)["hrf"], # block avg data
        quality  = lambda wc: all_hrf_files(wc)["quality"],    # quality metrics
        geo  = lambda wc: all_hrf_files(wc)["geo"]    # geometric pos and landmarks
    output:
        # group averaged data
        (f"{ROOT}/derivatives/{DERIV}/groupaverage/task-{{task}}_nirs_groupaverage_chanspace_{config['hrf_estimation']['rec_str']}.pkl")   
    params:
        cfg_groupaverage = config["groupaverage"],
        cfg_dataset = config['dataset'],
        cfg_hrf = config['hrf_estimation'],
        flag_prune_channels = config['preprocess']['steps']['prune']['enable'],
    script:
        "scripts/groupaverage.py"




def all_img_files(wc):
    ''' Return a list of all img .pkl files for this subject and task (i.e. all subs)'''  
    img_paths =  [
        (
        f"{ROOT}/derivatives/{DERIV}/image_results/sub-{subject}/"
        f"Xs_sub-{subject}_{wc.task}"
        f"_cov_alpha_spatial_{config['image_recon']['alpha_spatial']}"
        f"_alpha_meas_{config['image_recon']['alpha_meas']}"
        + ("_direct" if config["image_recon"]["DIRECT"]["enable"] else "_indirect")
        + ("_Cmeas" if config["image_recon"]["Cmeas"]["enable"] else "_noCmeas")
        + ("_SB" if config["image_recon"]["spatial_basis"]["enable"] else "_noSB")
        + ("_mag" if config["image_recon"]["mag"]["enable"] else "_ts")
        + (f"_{config['image_recon']['mag']['t_win'][0]}_{config['image_recon']['mag']['t_win'][1]}" if config["image_recon"]["mag"]["enable"] else "")
        + ".pkl.gz"
    )
        for subject in config["dataset"]["subject"]
    ]
    return {
        "image":  img_paths,
    }
        

use rule groupaverage as groupaverage_imgspace with:
    input:
        hrf_subs = lambda wc: all_img_files(wc)["image"],  # img data from all subs for this task
        quality  = lambda wc: all_hrf_files(wc)["quality"],    # quality metrics
        geo  = lambda wc: all_hrf_files(wc)["geo"]    # geometric pos and landmarks
    output:
        get_groupavg_imagerecon_output()
    params:
        cfg_img_recon = config['image_recon'],
        cfg_dataset = config['dataset']



rule imagerecon:
    input:
        hrf_data = rules.hrf_estimation.output.pickle,
        module4 = "scripts/modules/module_image_recon.py",
        module5 = "scripts/modules/module_spatial_basis_funs.py"
    output:
        get_imagerecon_output()
    params:
        cfg_img_recon = config['image_recon'],
        cfg_dataset = config['dataset'], 
        cfg_hrf = config['hrf_estimation']
    script:
        "scripts/image_recon.py"
        
        
